/*
 * This build file was generated by the Gradle 'init' task.
 *
 * This generated file contains a sample Java project to get you started.
 * For more details take a look at the Java Quickstart chapter in the Gradle
 * user guide available at https://docs.gradle.org/4.4.1/userguide/tutorial_java_projects.html
 */

plugins {
    id 'java'
    id 'com.google.protobuf' version '0.8.16'
    id 'idea'
    id 'maven-publish'
    id 'signing'
    id 'com.github.spotbugs' version '5.2.1'
}

archivesBaseName = 'durabletask-azuremanaged'
group 'com.microsoft'
version = '1.5.0-preview.1'

def grpcVersion = '1.59.0'
def azureCoreVersion = '1.45.0'
def azureIdentityVersion = '1.11.1'
// When build on local, you need to set this value to your local jdk11 directory.
// Java11 is used to compile and run all the tests.
def PATH_TO_TEST_JAVA_RUNTIME = System.env.JDK_11 ?: System.getProperty("java.home")

repositories {
    mavenCentral()
}

dependencies {
    implementation project(':client')
    implementation "io.grpc:grpc-protobuf:${grpcVersion}"
    implementation "io.grpc:grpc-stub:${grpcVersion}"
    runtimeOnly "io.grpc:grpc-netty-shaded:${grpcVersion}"

    implementation "com.azure:azure-core:${azureCoreVersion}"
    implementation "com.azure:azure-identity:${azureIdentityVersion}"

    // Test dependencies
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.10.0'
    testImplementation 'org.mockito:mockito-core:5.3.1'
    testImplementation 'org.mockito:mockito-junit-jupiter:5.3.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.10.0'
}

compileJava {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

compileTestJava {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
    options.fork = true
    options.forkOptions.executable = "${PATH_TO_TEST_JAVA_RUNTIME}/bin/javac"
}

test {
    useJUnitPlatform()
}

publishing {
    repositories {
        maven {
            url "file://$project.rootDir/repo"
        }
    }
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifactId = archivesBaseName
            pom {
                name = 'Durable Task Azure Managed SDK for Java'
                description = 'This package contains classes and interfaces for building Durable Task orchestrations in Java using Azure Managed mode.'
                url = "https://github.com/microsoft/durabletask-java/tree/main/azuremanaged"
                licenses {
                    license {
                        name = "MIT License"
                        url = "https://opensource.org/licenses/MIT"
                        distribution = "repo"
                    }
                }
                developers {
                    developer {
                        id = "Microsoft"
                        name = "Microsoft Corporation"
                    }
                }
                scm {
                    connection = "scm:git:https://github.com/microsoft/durabletask-java"
                    developerConnection = "scm:git:git@github.com:microsoft/durabletask-java"
                    url = "https://github.com/microsoft/durabletask-java/tree/main/azuremanaged"
                }
                withXml {
                    project.configurations.compileOnly.allDependencies.each { dependency ->
                        asNode().dependencies[0].appendNode("dependency").with {
                            it.appendNode("groupId", dependency.group)
                            it.appendNode("artifactId", dependency.name)
                            it.appendNode("version", dependency.version)
                            it.appendNode("scope", "provided")
                        }
                    }
                }
            }
        }
    }
}

signing {
    required = !project.hasProperty("skipSigning")
    sign publishing.publications.mavenJava
}

java {
    withSourcesJar()
    withJavadocJar()
}

spotbugs {
    toolVersion = '4.9.2'
    effort = 'max'
    reportLevel = 'high'
    ignoreFailures = true
    excludeFilter = file('spotbugs-exclude.xml')
}

spotbugsMain {
    reports {
        html {
            required = true
            stylesheet = 'fancy-hist.xsl'
        }
        xml {
            required = true
        }
    }
}

spotbugsTest {
    reports {
        html {
            required = true
            stylesheet = 'fancy-hist.xsl'
        }
        xml {
            required = true
        }
    }
}

