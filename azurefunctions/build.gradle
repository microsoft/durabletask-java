plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id 'com.github.spotbugs' version '5.2.1'
}

group 'com.microsoft'
version = '1.5.1'
archivesBaseName = 'durabletask-azure-functions'

def protocVersion = '3.12.0'

repositories {
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots/"
    }
}

dependencies {
    api project(':client')
    implementation group: 'com.microsoft.azure.functions', name: 'azure-functions-java-library', version: '3.0.0'
    implementation "com.google.protobuf:protobuf-java:${protocVersion}"
    compileOnly "com.microsoft.azure.functions:azure-functions-java-spi:1.0.0"
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

publishing {
    repositories {
        maven {
            url "file://$project.rootDir/repo"
        }
    }
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifactId = archivesBaseName
            pom {
                name = 'Azure Durable Functions SDK for Java'
                description = 'This package contains classes, interfaces, and annotations for developing with Azure Durable Functions in Java.'
                url = "https://github.com/microsoft/durabletask-java/tree/main/azurefunctions"
                licenses {
                    license {
                        name = "MIT License"
                        url = "https://opensource.org/licenses/MIT"
                        distribution = "repo"
                    }
                }
                developers {
                    developer {
                        id = "Microsoft"
                        name = "Microsoft Corporation"
                    }
                }
                scm {
                    connection = "scm:git:https://github.com/microsoft/durabletask-java"
                    developerConnection = "scm:git:git@github.com:microsoft/durabletask-java"
                    url = "https://github.com/microsoft/durabletask-java/tree/main/azurefunctions"
                }
                // use below script to include compile-only dependencies when generated pom file.
                // This is pain point when we onboard API docs as the missing compile-only dependencies crash the
                // API doc's team onboarding pipeline.
                withXml {
                    project.configurations.compileOnly.allDependencies.each { dependency ->
                        asNode().dependencies[0].appendNode("dependency").with {
                            it.appendNode("groupId", dependency.group)
                            it.appendNode("artifactId", dependency.name)
                            it.appendNode("version", dependency.version)
                            it.appendNode("scope", "provided")
                        }
                    }
                }
            }
        }
    }
}

signing {
   required = !project.hasProperty("skipSigning")
   sign publishing.publications.mavenJava
}

java {
    withSourcesJar()
    withJavadocJar()
}

spotbugs {
    toolVersion = '4.9.2'
    effort = 'max'
    reportLevel = 'high'
    ignoreFailures = true
    excludeFilter = file('spotbugs-exclude.xml')
}

spotbugsMain {
    reports {
        html {
            required = true
            stylesheet = 'fancy-hist.xsl'
        }
        xml {
            required = true
        }
    }
}

spotbugsTest {
    reports {
        html {
            required = true
            stylesheet = 'fancy-hist.xsl'
        }
        xml {
            required = true
        }
    }
}
